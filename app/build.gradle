apply plugin: 'maven-publish' // enable maven publication plugin
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization' //for JSON parsing
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount' // enable count of existing methods
apply plugin: 'androidx.navigation.safeargs' //enable safe arguments passing

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                developers {
                    developer {
                        id = 'archi39'
                        name = 'EvgenySamarin'
                        email = 'ey.samarin@gmail.com'
                    }
                }
            }
        }
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.example.cleanarchtemplate"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "SERVER_URL", '"https://api.github.com"')
        buildConfigField("String", "SERVER_UPLOAD", '"https://api.github.com"')
        buildConfigField("boolean", "MOCKS", 'false')
        signingConfig signingConfigs.debug
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //apply proguard rules for external libs. All proguard files must be kept into proguard
            //directory, which locate into module directory
            proguardFiles fileTree('proguard').asList().toArray()
        }

        mock {
            buildConfigField("boolean", "MOCKS", 'true')
            debuggable true
            //BACKLOG proguard strangle of kotlinx serialization you should resolve this
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard').asList().toArray()
        }
    }

    //for support AppBarConfiguration in BaseActivity
    kotlinOptions {
        jvmTarget = "1.8"
    }

    //to support java 1.8 capabilities
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    kapt librariesKapt
    implementation libraries
    implementation librariesBase
    testImplementation testLibraries
    androidTestImplementation androidTestLibraries
}
